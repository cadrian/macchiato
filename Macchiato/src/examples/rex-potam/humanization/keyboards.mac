#!/usr/bin/macchiato
/*
 * This file is part of Macchiato.
 *
 * Macchiato is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 of the License.
 *
 * Macchiato is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Macchiato.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

/*
 * This is the humanization for keyboards.
 *
 * In fact, it is a bit badly named; should be for SOLOISTS. Notes run
 * a bit wilder, except chords that are tighter.
 */

import itf "interface.mac";
import utl "utils.mac";

def humanize(value, anchor, percent) {
    if value == 0 {
        Result = 0;
    } else {
        Result = utl.Anchored(value, anchor, percent);
        if Result <= 0 {
            Result = 1;
        }
    }
}

def range(bpm, percent) {
    Result = bpm * 2 / percent;
    if Result < percent {
        Result = percent;
    }
}

def tick(tick, bpm, percent, sign, canCollide) {
    local r = range(bpm, percent);
    if (sign == 0) {
        // +/- 'percent'% bpm
        Result = tick + random(r) - r;
    } else {
        // 10% bpm * sign with a slight offset to allow small collisions
        local t = random(r / 2);
        if canCollide {
            local m = (r * percent) / 80;
            if m > 0 {
                t = t - random(m);
            }
        }
        Result = tick + t * sign;
    }
    if Result < 1 {
        Result = 1;
    }
}

class runningRecord {

    In;
    Out;

    def runningRecord(i, o) {
        In = i;
        Out = o;
    }

}

class KeyboardsHumanizer (itf.Humanizer) {

    // current track number
    tracknum;

    // portamento pedal state (true is down)
    portamento;

    // beats per minute
    bpm;

    // current velocity, per channel, per pitch
    velocity;

    // current expression, per channel
    expression;

    // current volume, per channel
    volume;

    // currently running notes per channel
    // For each channel, the array index is the pitch of the note;
    // its value is the start tick of the note
    running;
    // actual start of notes for a same chord
    start;
    // actual end of notes for a same chord
    stop;

    // note lengths per track, per channel
    // For each track, each channel, the array index is the start tick of each note;
    // its value is an array: index = pitch, value = length
    notes;

    // configuration: velocity wildness (in %)
    percent_velocity;

    // configuration: ticks wildness (in % of BPM)
    percent_tick;

    def KeyboardsHumanizer(pv, pt) {
        tracknum = 0;
        percent_velocity = pv;
        percent_tick = pt;
        print("v:" + toString(percent_velocity) + "% - t:" + toString(percent_tick) + "%");
    }

    def StartSequence() {
        print("Running keyboards humanizer");
    }

    def EndSequence() {
        write("record", record);
        write("notes", notes);
    }

    def StartTrack() {
        velocity = [[], [], [], [], [], [], [], [],
                    [], [], [], [], [], [], [], []];

        expression = [127, 127, 127, 127, 127, 127, 127, 127,
                      127, 127, 127, 127, 127, 127, 127, 127];

        volume = [127, 127, 127, 127, 127, 127, 127, 127,
                  127, 127, 127, 127, 127, 127, 127, 127];

        running = [[], [], [], [], [], [], [], [],
                   [], [], [], [], [], [], [], []];

        start = [];

        stop = [];

        notes[tracknum] = [[], [], [], [], [], [], [], [],
                           [], [], [], [], [], [], [], []];

        portamento = false;

        bpm = 100;
    }

    def EndTrack() {
        record[tracknum] = {
            "velocity": velocity,
            "expression": expression
        };
        tracknum = tracknum + 1;
    }

    def TrackNumber() {
        Result = tracknum;
    }

    def Tempo(Event) {
        bpm = Event.Bpm;
    }

    def NoteOn(Event) {
        // Record the velocity
        velocity[Event.Channel][Event.Pitch] = Event.Velocity;

        if (Event.Channel == 9) or (volume[Event.Channel] > 0) {
            // Emit an Event with a velocity that depends on the expression,
            // and with a bit of random for rough "humanization"
            local v = velocity[Event.Channel][Event.Pitch];
            local e = expression[Event.Channel];
            if (v > 0) and (e > 0) {
                v = (humanize(v, v, percent_velocity) * e) / 127;
                local t;
                if start[Event.Tick] exists {
                    t = tick(start[Event.Tick], bpm, percent_tick / 2, 1,
                             (running[Event.Channel][Event.Pitch] exists) and (running[Event.Channel][Event.Pitch].In > 0));
                } else {
                    t = tick(Event.Tick, bpm, percent_tick, 1,
                             (running[Event.Channel][Event.Pitch] exists) and (running[Event.Channel][Event.Pitch].In > 0));
                    start[Event.Tick] = t;
                }
                running[Event.Channel][Event.Pitch] = runningRecord(Event.Tick, t);
                emit NOTE_ON(Event.Channel, Event.Pitch, v) at t;
            } else {
                velocity[Event.Channel][Event.Pitch] = 0;
            }
        }
    }

    def NoteOnOff(Event) {
        if (Event.Channel != 9) and (volume[Event.Channel] == 0) {
            next;
        }

        // Emit an Event with a velocity that depends on the expression,
        // and with a bit of random for rough "humanization"
        // Note that expression may have changed so the release velocity is not
        // necessarily equal to the press velocity
        local t;
        if stop[Event.Tick] exists {
            t = tick(stop[Event.Tick], bpm, percent_tick / 2, 1,
                     (running[Event.Channel][Event.Pitch] exists) and (running[Event.Channel][Event.Pitch].In > 0));
        } else {
            t = tick(Event.Tick, bpm, percent_tick, -1,
                     (running[Event.Channel][Event.Pitch] exists) and (running[Event.Channel][Event.Pitch].In > 0));
            stop[Event.Tick] = t;
        }
        if portamento {
            velocity[Event.Channel][Event.Pitch] = t;
            emit NOTE_ON(Event.Channel, Event.Pitch, 0) at t;
        } else if velocity[Event.Channel][Event.Pitch] > 0 {
            if t < running[Event.Channel][Event.Pitch].Out {
                if t < running[Event.Channel][Event.Pitch].In {
                    print("**** note-on after actual note-off!!");
                    t = running[Event.Channel][Event.Pitch].In + bpm / percent;
                } else {
                    print("**** note-off before note-on, using real value");
                    t = Event.Tick;
                }
            }
            local v = velocity[Event.Channel][Event.Pitch];
            local e = expression[Event.Channel];
            v = (humanize(v, v, percent_velocity) * e) / 127;
            record_note_length(Event.Channel, Event.Pitch, t);
            running[Event.Channel][Event.Pitch] = runningRecord(-1, -1);
            emit NOTE_OFF(Event.Channel, Event.Pitch, v) at t;
        }
    }

    def record_note_length(channel, pitch, tick) {
        if running[channel][pitch] exists {
            local start = running[channel][pitch].Out;
            if start > 0 {
                local length = tick - start;
                notes[tracknum][channel][start][pitch] = length;
            }
        }
    }

    def NoteOff(Event) {
        // For completion only -- because NoteWorthy Composer
        // does not emit those Events

        if (volume[Event.Channel] > 0) and (velocity[Event.Channel][Event.Pitch] > 0) {
            // Emit an Event with a velocity that depends on the expression,
            // and with a bit of random for rough "humanization"
            // Note that expression may have changed so the release velocity is not
            // necessarily equal to the press velocity
            local v = (Event.Velocity * expression[Event.Channel]) / 127;
            v = humanize(v, v, percent_velocity);
            local t;
            if stop[Event.Tick] exists {
                t = tick(stop[Event.Tick], bpm, percent_tick / 2, -1,
                         (running[Event.Channel][Event.Pitch] exists) and (running[Event.Channel][Event.Pitch].In > 0));
            } else {
                t = tick(Event.Tick, bpm, percent_tick, -1,
                         (running[Event.Channel][Event.Pitch] exists) and (running[Event.Channel][Event.Pitch].In > 0));
            }
            if t < running[Event.Channel][Event.Pitch].Out {
                if t < running[Event.Channel][Event.Pitch].In {
                    print("**** note-on after actual note-off!!");
                    t = running[Event.Channel][Event.Pitch].In + bpm / percent;
                } else {
                    print("**** note-off before note-on, using real value");
                    t = Event.Tick;
                }
            }
            record_note_length(Event.Channel, Event.Pitch, t);
            velocity[Event.Channel][Event.Pitch] = 0;
            running[Event.Channel][Event.Pitch] = runningRecord(-1, -1);
            emit NOTE_OFF(Event.Channel, Event.Pitch, v) at t;
        }
    }

    def Expression(Event) {
        // Record the expression value
        expression[Event.Channel] = Event.Value;
    }

    def Volume(Event) {
        // Record the volume value
        volume[Event.Channel] = Event.Value;
    }

    def Portamento(Event) {
        portamento = Event.Value
    }

    def PortamentoTime(Event) {
        // humanize this Event too
        local v = Event.Value;
        v = humanize(v, v, percent_velocity);
        local t = tick(Event.Tick, bpm, percent_tick / 2, 0, false);
        emit CONTROL_CHANGE(Event.Channel, PORTAMENTO_TIME, v) at t;
    }

}
