#!/usr/bin/macchiato
/*
 * This file is part of Macchiato.
 *
 * Macchiato is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 of the License.
 *
 * Macchiato is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Macchiato.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

/*
 * Advanced example, used by Rex Potam
 * http://rex-potam.cadrian.net/
 *
 * This ruleset fixes a few shortcomings of the MIDI production chain
 * used by the songwriter:
 *
 * - NoteWorthy Composer emits NOTE_ON(velocity=0) instead of NOTE_OFF
 *   Events -- replace by NOTE_OFF, except if the PORTAMENTO pedal is on
 *
 * - NoteWorthy Composer emits all tracks, even if they are muted --
 *   an input file may be used to map the actual channels to some
 *   useful value
 *
 * - fluidsynth uses the Expression MPC as a simple volume switch,
 *   whereas Rex uses it for actual dynamics, and rough velocities for
 *   musicality (e.g. strong vs weak beats, musical phrases); these
 *   rules combine the expression and velocity to emit actual note
 *   velocities
 *
 * This also adds a bit of random for "humanization".
 */

import h "rex-potam/humanization.mac";

def loadChannelArray(arg) {
    local p = /map=(?<value>.*)/;
    local m = p.Matcher(arg);
    if m.Matches() {
        local value = m.Group("value");
        channel_array = System.FromString(value);
    }
}

def loadTracks(arg) {
    local p = /tracks=(?<value>.*)/;
    local m = p.Matcher(arg);
    if m.Matches() {
        local value = m.Group("value");
        tracks = System.FromString(value);
    }
}

def checkTrackNumber(tn, val) {
    Result = val or (tn == humanizer.TrackNumber());
}

def isValidTrack() {
    if tracks.Size() == 0 {
        Result = true;
    } else {
        Result = tracks.Map(checkTrackNumber, false);
    }
}

def fixEventChannel() {
    if Event.Channel exists {
        local c = channel_array[Event.Channel]
        if c exists {
            if c < 0 {
                // forget this Event as it is not mapped
                next;
            }
            Event.Channel = c;
        }
    }
}

true {
    fixEventChannel();
}

BEGIN SEQUENCE {
    channel_array = [];
    tracks = [];
    Arguments.ForEach(loadChannelArray);
    Arguments.ForEach(loadTracks);
    System.Print("channel_array=" + System.ToString(channel_array));
    System.Print("tracks=" + System.ToString(tracks));
    humanizer = h.LoadHumanizer();
    humanizer.StartSequence();
}

BEGIN TRACK {
    humanizer.StartTrack();
    System.Print("Starting track: " + System.ToString(humanizer.TrackNumber()));
    if (not isValidTrack()) {
        // skip the track
        next;
    }
}

(Event.Type == CONTROL_CHANGE) and (Event.Mpc == PORTAMENTO) {
    humanizer.Portamento(Event);
}

(Event.Type == CONTROL_CHANGE) and (Event.Mpc == PORTAMENTO_TIME) {
    humanizer.PortamentoTime(Event);
    next;
}

(Event.Type == CONTROL_CHANGE) and (Event.Mpc == EXPRESSION) {
    humanizer.Expression(Event);
    next;
}

(Event.Type == CONTROL_CHANGE) and (Event.Mpc == CHANNEL_VOLUME) {
    humanizer.Volume(Event);
    emit;
    next;
}

(Event.Type == PROGRAM_CHANGE) {
    // System.Print("Program change on channel " + System.ToString(Event.Channel));
}

(Event.Type == TEMPO) {
    humanizer.Tempo(Event);
    emit;
    next;
}

(Event.Type == TIME_SIGNATURE) {
    humanizer.TimeSignature(Event);
    emit;
    next;
}

(Event.Type == PROGRAM_CHANGE) and (Event.Channel == 9) {
    // Don't emit program change on channel "10" (9 actually)
    System.Print("Ignore program change on drums channel");
    next;
}

(Event.Type == NOTE_ON) and (Event.Velocity > 0) {
    humanizer.NoteOn(Event);
    next;
}

(Event.Type == NOTE_ON) and (Event.Velocity == 0) {
    humanizer.NoteOnOff(Event);
    next;
}

(Event.Type == NOTE_OFF) {
    humanizer.NoteOff(Event);
    next;
}

(Event.Type == TEXT) {
    System.Print("TEXT: " + Event.Text);
}

(Event.Type == COPYRIGHT) {
    System.Print("COPYRIGHT: " + Event.Text);
}

(Event.Type == TRACK_NAME) {
    System.Print("TRACK_NAME: " + Event.Text);
}

(Event.Type == INSTRUMENT_NAME) {
    System.Print("INSTRUMENT_NAME: " + Event.Text);
}

(Event.Type == LYRICS) {
    System.Print("LYRICS: " + Event.Text);
}

(Event.Type == MARKER_TEXT) {
    System.Print("MARKER_TEXT: " + Event.Text);
}

(Event.Type == CUE_POINT) {
    System.Print("CUE_POINT: " + Event.Text);
}

END TRACK {
    humanizer.EndTrack();
}

END SEQUENCE {
    humanizer.EndSequence();
}
