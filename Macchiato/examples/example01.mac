# Called at each beginning of track. Other special markers are BEGIN SEQUENCE, END SEQUENCE, and END TRACK.
BEGIN TRACK {
    # reset global variables at track beginning
    
    # velocity records the velocity of playing notes, per channel, per pitch
	velocity = [];
	
	# expression records the current expression, per channel 
	expression = [];
	
	# fill default values
	channel = 0;
	while (channel < 16) {
		velocity[channel] = [];
		expression[channel] = 127;
		channel = channel + 1;
	}
}

(event.type == CONTROL_CHANGE) and (event.mpc == EXPRESSION) {
    # record the expression value
    expression[event.channel] = event.value;
	# next "skips" the remainder of recipes and analyzes the next event
	# (in this case the old expression event is not emitted at all)
    next;
}

(event.type == NOTE_ON) and (event.velocity > 0) {
	velocity[event.channel][event.pitch] = event.velocity;
	emit NOTE_ON(event.channel, event.pitch, (velocity[event.channel][event.pitch] * expression[event.channel]) / 127)
		at event.tick + random(5);
	next;
}

(event.type == NOTE_ON) and (event.velocity == 0) {
	# emits a new event (instead of the analyzed one)
	# Note that expression may have changed so the release velocity is not
	# necessarily equal to the press velocity
	emit NOTE_OFF(event.channel, event.pitch, (velocity[event.channel][event.pitch] * expression[event.channel]) / 127)
		at event.tick - random(5);
	next;
}

(event.type == NOTE_OFF) {
	emit NOTE_OFF(event.channel, event.pitch, (event.velocity * expression[event.channel]) / 127)
		at event.tick - random(5);
	next;
}

# the current event is emitted if there were no next before
