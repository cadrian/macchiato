# Called at each beginning of track. Other special markers are BEGIN SEQUENCE, END SEQUENCE, and END TRACK.
BEGIN TRACK {
    # reset global variables at track beginning
	notes = [];
	expression = [];
	i = 0;
	while (i < 16) {
		expression[i] = 127;
		i = i + 1;
	}
}

(event.type == CONTROL_CHANGE) and (event.mpc == EXPRESSION) {
    # record the expression value
    expression[event.channel] = event.value;
	# next "skips" the remainder of recipes and analyzes the next event
	# (in this case the old expression event is not emitted at all)
    next;
}

(event.type == NOTE_ON) and (event.velocity > 0) {
	notes[event.pitch] = event.velocity;
	emit NOTE_ON(event.channel, event.pitch, (event.velocity * expression[event.channel]) / 127);
	next;
}

(event.type == NOTE_ON) and (event.velocity == 0) {
	# emits a new event (instead of the analyzed one)
	# Note that expression may have changed so the release velocity is not
	# necessarily equal to the press velocity
	emit NOTE_OFF(event.channel, event.pitch, (notes[event.pitch] * expression[event.channel]) / 127);
	next;
}

(event.type == NOTE_OFF) {
	emit NOTE_OFF(event.channel, event.pitch, (event.velocity * expression[event.channel]) / 127);
	next;
}

# the event is emitted if there were no next before
